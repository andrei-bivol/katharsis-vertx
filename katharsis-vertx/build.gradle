import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'idea'
    id "com.jfrog.bintray" version "1.6"
    id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"

    compile "io.katharsis:katharsis-core:${katharsisVersion}"
    compile "io.vertx:vertx-core:${vertxVersion}"
    compile "io.vertx:vertx-web:${vertxVersion}"

    compile "org.reflections:reflections:${reflectionsVersion}"

    compile "org.slf4j:slf4j-api:${slf4jVersion}"

    testCompile "io.vertx:vertx-unit:${vertxVersion}"
    testCompile "junit:junit:4.11"
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['katharsis']

    publish = true

    pkg {
        repo = 'maven'
        name = 'katharsis-vertx'
        userOrg = 'netdava'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/katharsis-project/katharsis-vertx'
        websiteUrl = 'http://katharsis.io/'
        desc = 'Elegant and powerful HATEOAS for Java based on JSON API standard'
        issueTrackerUrl = 'https://github.com/katharsis-project/katharsis-vertx/issues'
        labels = ['java', 'json-api', 'rest', 'web', 'api', 'services']
        publicDownloadNumbers = true
        githubRepo = 'katharsis-project/katharsis-vertx'
        version {
            name = version
            desc = 'Vertx integration for Katharsis json-api library'
            released = new SimpleDateFormat('yyyy-MM-dd\'T\'HH:mm:ss.SSSZZ').format(new Date())
            vcsTag = version
        }
    }
}

publishing {
    publications {
        katharsis(MavenPublication) {
            groupId 'com.netdava'
            artifactId 'katharsis-vertx'
            from components.java
        }
    }
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.compileOnly]
    }
}
